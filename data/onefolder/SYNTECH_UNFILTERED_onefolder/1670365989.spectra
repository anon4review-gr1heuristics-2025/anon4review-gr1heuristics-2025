import "./imports/DwyerPatterns.spectra"

spec Patrolling

define DX := 4;
define DY := 4;

type DimX = Int(0..DX);
type DimY = Int(0..DY);



sys DimX robotX;
sys DimY robotY;

//TODO: complete the specification


//Part 1: the robot will always eventually visit all checkpoints

env DimX AX;
env DimY AY;
env DimX BX;
env DimY BY;
env DimX CX;
env DimY CY;

predicate robotAt(DimX x, DimY y) :
	robotX = x & robotY = y;

predicate pointEql(DimX x1, DimY y1, DimX x2, DimY y2) :
	x1 = x2 & y1 = y2;	

predicate stay(DimX x, DimY y) :
	x = next(x) & y = next(y);
	
gar robotMove: alw
	(robotX + 1 = next(robotX) & robotY = next(robotY)) |
	(robotX - 1 = next(robotX) & robotY = next(robotY)) |
	(robotX = next(robotX) & robotY + 1 = next(robotY)) |
	(robotX = next(robotX) & robotY - 1 = next(robotY)) |
	(robotX = next(robotX) & robotY  = next(robotY)); 
	
	
asm alw	stay(AX, AY);
asm alw	stay(BX, BY);
asm alw	stay(CX, CY);

asm ini AX != BX | AY !=BY;
asm ini CX != BX | CY !=BY;
asm ini AX != CX | AY !=CY;



gar alwEv robotAt(AX, AY);
gar alwEv robotAt(BX, BY);
gar alwEv robotAt(CX, CY);



//Part 2: Whenever the robot visits a target, it should eventually also visit the top left corner.
//We assume that the meaning of this requirement  is that after visiting a target, 
//it eventually visits the corner, but it can visit other targets along the way

gar alwEv robotAt(0,0);

asm ini AX != 0 | AY !=0;
asm ini BX != 0 | BY !=0;
asm ini CX != 0 | CY !=0;

asm ini AX != 4 | AY !=0;
asm ini BX != 4 | BY !=0;
asm ini CX != 4 | CY !=0;

//Part 3: 

env boolean Ablocked;
env boolean Bblocked;
env boolean Cblocked;

asm alwEv !Ablocked;
asm alwEv !Bblocked;
asm alwEv !Cblocked;

//asm alw PREV(robotAt(AX, AY)) -> !Ablocked;
//asm alw PREV(robotAt(BX, BY)) -> !Bblocked;
//asm alw PREV(robotAt(CX, CY)) -> !Cblocked;

predicate inRange(DimX x1, DimY y1, DimX x2, DimY y2):
	x1 -1 <= x2 & x2 <= x1 + 1 &
	y1 -1 <= y2 & y2 <= y1 + 1;
	

asm alw inRange(AX, AY, robotX, robotY) -> (!Ablocked -> !next(Ablocked));
asm alw inRange(BX, BY, robotX, robotY) -> (!Bblocked -> !next(Bblocked));
asm alw inRange(CX, CY, robotX, robotY) -> (!Cblocked -> !next(Cblocked));

gar alw Ablocked -> !next(robotAt(AX, AY));
gar alw Bblocked -> !next(robotAt(BX, BY));
gar alw Cblocked -> !next(robotAt(CX, CY));

//Part 4 Occasionally, the robot starts losing power. In this case, it has to eventually go to the top right corner for charging and stay there for at least 4 states.0.
env boolean isCharged;

counter charging(0..3) {
	charging = 0;
	inc: robotAt(4,0);
	reset: !robotAt(4,0);
	overflow: keep;
}

gar alw (robotAt(4,0) & charging < 3) -> stay(robotX, robotY);

asm alw charging = 3 -> next(isCharged);



gar P_becomes_true_after_Q_until_R(robotAt(4, 0), !isCharged, charging=3);

//Part 5: Obstacles: the robot should avoid the obstacles (we define them in advance)

define Obst1x := 1;
define Obst1y := 1;
define Obst2x := 3;
define Obst2y := 3;
define Obst3x := 2;
define Obst3y := 1;
define Obst4x := 0;
define Obst4y := 3;
define Obst5x := 3;
define Obst5y := 4;


gar alw robotX != Obst1x | robotY != Obst1y;
gar alw robotX != Obst2x | robotY != Obst2y;
gar alw robotX != Obst3x | robotY != Obst3y;
gar alw robotX != Obst4x | robotY != Obst4y;
gar alw robotX != Obst5x | robotY != Obst5y;

asm ini 
	(AX != Obst1x | AY != Obst1y) &
	(AX != Obst2x | AY != Obst2y) &
	(AX != Obst3x | AY != Obst3y) &
	(AX != Obst4x | AY != Obst4y) &
	(AX != Obst5x | AY != Obst5y);
	
asm ini 
	(BX != Obst1x | BY != Obst1y) &
	(BX != Obst2x | BY != Obst2y) &
	(BX != Obst3x | BY != Obst3y) &
	(BX != Obst4x | BY != Obst4y) &
	(BX != Obst5x | BY != Obst5y);
	
asm ini                         
	(CX != Obst1x | CY != Obst1y) &
	(CX != Obst2x | CY != Obst2y) &
	(CX != Obst3x | CY != Obst3y) &
	(CX != Obst4x | CY != Obst4y) &
	(CX != Obst5x | CY != Obst5y); 
	