import "./imports/DwyerPatterns.spectra"

spec Patrolling

define DX := 4;
define DY := 4;
define num_targets := 3;
define num_static_obstacles := 5;
define atCharger := robotX=4 and robotY=0;

type DimX = Int(0..DX);
type DimY = Int(0..DY);

sys DimX robotX;
sys DimY robotY;

env DimX[num_targets] goalsX;
env DimY[num_targets] goalsY;
env DimX movingObstacleX;
env boolean[num_targets] redGoals;
env boolean charged;

define staticObstaclesX[num_static_obstacles] := {1,3,2,0,3};
define staticObstaclesY[num_static_obstacles] := {1,3,1,3,4};
define movingObstacleY := 2;

predicate moveDistX(DimX pos):
  pos+1 = next(pos) |
  pos   = next(pos) |
  pos-1 = next(pos);

predicate moveDistY(DimY pos):
  pos+1 = next(pos) |
  pos   = next(pos) |
  pos-1 = next(pos);

predicate fixed(DimX x, DimY y):
  x = next(x) & y = next(y);

predicate dontStayDistX(DimX pos):
  pos+1 = next(pos) |
  pos-1 = next(pos);

// robot is at goal i	
predicate robotAtGoal(Int(0..(num_targets-1)) i):
  robotX = goalsX[i] & robotY = goalsY[i];

// moving obstacle is at goal i
predicate movingObstacleAtGoal(Int(0..(num_targets-1)) i):
  movingObstacleX = goalsX[i] & movingObstacleY = goalsY[i];

gar robotMoveDist:
  alw moveDistX(robotX) and moveDistY(robotY);

/* Goals assumptions */
asm goalsAreDifferent:
  ini (goalsX[0] != goalsX[1] or goalsY[0] != goalsY[1]) and
      (goalsX[0] != goalsX[2] or goalsY[0] != goalsY[2]) and
      (goalsX[1] != goalsX[2] or goalsY[1] != goalsY[2]);

asm goalsDontMove:
  alw fixed(goalsX[0], goalsY[0]) and
      fixed(goalsX[1], goalsY[1]) and
      fixed(goalsX[2], goalsY[2]);

/* Goals & obstacles assumptions */
asm goalsNotOnObstacles:
  ini (goalsX[0] != staticObstaclesX[0] or goalsY[0] != staticObstaclesY[0]) and
      (goalsX[0] != staticObstaclesX[1] or goalsY[0] != staticObstaclesY[1]) and
      (goalsX[0] != staticObstaclesX[2] or goalsY[0] != staticObstaclesY[2]) and
      (goalsX[0] != staticObstaclesX[3] or goalsY[0] != staticObstaclesY[3]) and
      (goalsX[0] != staticObstaclesX[4] or goalsY[0] != staticObstaclesY[4]) and
      (goalsX[1] != staticObstaclesX[0] or goalsY[1] != staticObstaclesY[0]) and
      (goalsX[1] != staticObstaclesX[1] or goalsY[1] != staticObstaclesY[1]) and
      (goalsX[1] != staticObstaclesX[2] or goalsY[1] != staticObstaclesY[2]) and
      (goalsX[1] != staticObstaclesX[3] or goalsY[1] != staticObstaclesY[3]) and
      (goalsX[1] != staticObstaclesX[4] or goalsY[1] != staticObstaclesY[4]) and
      (goalsX[2] != staticObstaclesX[0] or goalsY[2] != staticObstaclesY[0]) and
      (goalsX[2] != staticObstaclesX[1] or goalsY[2] != staticObstaclesY[1]) and
      (goalsX[2] != staticObstaclesX[2] or goalsY[2] != staticObstaclesY[2]) and
      (goalsX[2] != staticObstaclesX[3] or goalsY[2] != staticObstaclesY[3]) and
      (goalsX[2] != staticObstaclesX[4] or goalsY[2] != staticObstaclesY[4]);

asm firstGoalIsReachable:
  alwEv !redGoals[0] and !movingObstacleAtGoal(0);
asm secondGoalIsReachable:
  alwEv !redGoals[1] and !movingObstacleAtGoal(1);
asm thirdGoalIsReachable:
  alwEv !redGoals[2] and !movingObstacleAtGoal(2); 

asm goalsNotRedWhileRobotOnIt:
  alw (robotAtGoal(0) implies !next(redGoals[0])) and
      (robotAtGoal(1) implies !next(redGoals[1])) and
      (robotAtGoal(2) implies !next(redGoals[2]));

/* Obstacles only assumptions */

asm movingObstacleRespectRobot:
  alw !(next(movingObstacleX) = robotX and next(movingObstacleY) = robotY);

/* Charging */

//gar S_responds_to_P_globally(atCharger, !charged);
 
counter stepsFromUncharged(0..20) {
  stepsFromUncharged = 0;
  inc: !charged;
  reset: atCharger;
}

gar alw stepsFromUncharged < 20;

counter chargingTime(0..4) {
  chargingTime = 0;
  inc: atCharger;
  reset: !atCharger;
  overflow: keep;
}

gar alw (atCharger and (chargingTime < 4)) implies next(atCharger);

/* End of charging mechanism */

gar visitFirstGoalInifintelyOften:
 alwEv robotAtGoal(0);
gar visitSecondGoalInifintelyOften:
 alwEv robotAtGoal(1);
gar visitThirdGoalInifintelyOften:
 alwEv robotAtGoal(2);

gar visitTopLeftCornerInifintelyOften:
  alwEv robotX = 0 and robotY = 0;

gar avoidStaticObstacles:
  alw (robotX != staticObstaclesX[0] or robotY != staticObstaclesY[0]) and
      (robotX != staticObstaclesX[1] or robotY != staticObstaclesY[1]) and
      (robotX != staticObstaclesX[2] or robotY != staticObstaclesY[2]) and
      (robotX != staticObstaclesX[3] or robotY != staticObstaclesY[3]) and
      (robotX != staticObstaclesX[4] or robotY != staticObstaclesY[4]);

gar avoidMovingObstacle:
  alw (robotX != movingObstacleX or robotY != movingObstacleY);

gar avoidRedGoals:
  alw (redGoals[0] implies !robotAtGoal(0)) and
      (redGoals[1] implies !robotAtGoal(1)) and
      (redGoals[2] implies !robotAtGoal(2));
